!!!!!!!!!!!!!!!
!! ATTRIBUTE CATEGORIES
!!!!!!!!!!!!!!!

number = sg pl du sp;
gender = m f mf;
tense = pres past fut ifi pri fti;
person = p1 p2 p3 ;
case = nom gen acc par ine ela ill ade abl all loc com abe tra ess lat ;
tensemood = cond pot imp pres pri past ;
sem = sem_dummytag ;
nptype = ant top cog al ;
numtype = card ord ;
comparison = (COMP pst) pst comp sup COMP ;
voice = (VOICE actv) actv pasv VOICE ;
clitics = qst foc_uv ;
inf = inf infa infe infma ;
pcp = prfprc pp pprs ;

!!!!!!!!!!!!!!!
!! OUTPUT PATTERNS
!!!!!!!!!!!!!!!

n: _.number.case;
prn: _.number.case;
num: _.numtype.number.case;
adj: _.comparison.number.case;
np: _.nptype.gender.number.case;
vaux: _.voice.tensemood.person.number;
vblex: _.voice.tensemood.person.number;
vblex_inf: _.voice.inf.number.case;
vblex_pcp: _.voice.pcp.comparison.number.case;
vblex_massa: _.<actv>.<infma>.<sg>.<ine>;
ij: _;
adv: _;
cnjcoo: _;
cnjsub: _;
post: _;
pr: _;
pcle: _;

ADP: _;
ADV: _;
A: _;
IJ: _;
N: _.case;
PRN: _.case;
V: _;
C: _;
NUM: _;

S: _;
VP: _;
NP: _.case;
NUMP: _;

!!!!!!!!!!!!!!!
!! TAG-REWRITE RULES
!!!!!!!!!!!!!!!

tensemood > tensemood: pres pri ;
case > case: loc ine, acc gen ;
inf > inf: inf infa ; 
pcp > pcp: prfprc pp ;

!!!!!!!!!!!!!!!
!! MACROS
!!!!!!!!!!!!!!!


!!!!!!!!!!!!!!!
!! REDUCTION RULES
!!!!!!!!!!!!!!!

S -> VP NP { 1 _ 2 } ;
VP -> NP.loc lee@vblex { 1[case=ade] _ 2[number=sg] } |
      PRN.loc lee@vblex { 1[case=ade] _ 2[number=1.number] } |
      NP VP { 1 _ 2 } ;
NUMP -> num n ?(1.lem in ones) { 1 _ 2 } |
        num n { 1 _ 2[number=sg,case=par] } ;
NP -> %N { %1 } ;

V -> lee@vblex vblex.*.actio.ess { 1 _ 2(vblex_massa) } |
     lee@vblex vblex.*.prfprc { 1 _ 2(vblex_pcp)[number=sg,case=nom] } |
     vblex.*.inf { 1(vblex_inf)[number=sg,case=lat] } |
     vblex.*.prfprc { 1(vblex_pcp)[number=sg,case=nom] } |
     vblex { 1 } |
     vaux { 1 } ;
PRN -> %prn { %1 } ;
N -> n.*.cmp %n { 1[number=sg,case=gen] 2 } |
     %n { %1[number=(
            if (1.case=com) ""
            elif (1.case=ess) "sg" 
            else 1.number)] } |
     %np { %1 } ;
A -> adj.*.attr { 1[case=nom,number=sg] } |
     adj { 1 } ;
IJ -> ij { 1 } |
      pcle { 1 } ;
ADV -> adv { 1 } ;
NUM -> num { 1 } ;
ADP -> post { 1 } |
       pr { 1 } ;

C -> cnjcoo { 1 } |
     cnjsub { 1 } ;

